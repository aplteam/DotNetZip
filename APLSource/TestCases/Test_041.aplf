 r←Test_041(stopFlag batchFlag);⎕TRAP;path;zipFilename;targetFolder;myZip
⍝ Create a new Zip archive, add some files and then update one of them
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 r←T._Failed

 path←∆GetTestFolderInTemp 1⊃⎕SI
 F.MkDir path,'/src'
 F.MkDir path,'/unzipped'
 (path,'/src/')⎕NCOPY F.ExpandPath'./TestData\level-1-a.txt'
 (path,'/src/')⎕NCOPY F.ExpandPath'./TestData\level-1-b.txt'

 zipFilename←path,'/MyZipFile.zip'
 myZip←⎕NEW ##.DotNetZip(,⊂zipFilename)

 path myZip.Add'src/level-1-a.txt'
 path myZip.Add'src/level-1-b.txt'

 (⊂100⍴' ',1⊃⎕SI)F.NPUT(path,'/src/level-1-b.txt')1
 path myZip.Update'/src/level-1-b.txt'

 myZip.Dispose
 zipFilename ##.DotNetZip.UnzipTo path,'/unzipped'

 →T.GoToTidyUp(100⍴' ',1⊃⎕SI){⍺≢(≢⍺)↑⍵}1⊃F.NGET path,'/src/level-1-b.txt'

 r←T._OK

∆TidyUp:
 myZip.Dispose
 Assert 0=1⊃F.RmDir path
⍝Done
