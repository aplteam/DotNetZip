 r←Test_041(stopFlag batchFlag);⎕TRAP;path;zipFilename;targetFolder;myZip
⍝ Create a new Zip archive, add some files and then update one of them by calling `Add` again`
 ⎕TRAP←(999 'C' '∘∘∘ ⍝ Deliberate error')(0 'N')
 r←T._Failed

 path←∆GetTestFolderInTemp 1⊃⎕SI
 F.MkDir path,'/src'
 F.MkDir path,'/unzipped'
 (F.ExpandPath'./TestData\level-1-a.txt')F.CopyTo path,'/src/'
 (F.ExpandPath'./TestData\level-1-b.txt')F.CopyTo path,'/src/'

 zipFilename←path,'/MyZipFile.zip'
 myZip←⎕NEW ##.DotNetZip(,⊂zipFilename)

 path myZip.Add'src/level-1-a.txt'
 path myZip.Add'src/level-1-b.txt'

 (⊂100⍴' ',1⊃⎕SI)F.NPUT(path,'/src/level-1-b.txt')1
 path myZip.Add'/src/level-1-b.txt'

 myZip.Dispose
 zipFilename ##.DotNetZip.UnzipTo path,'/unzipped'

 →T.GoToTidyUp(100⍴' ',1⊃⎕SI){⍺≢(≢⍺)↑⍵}1⊃F.NGET path,'/src/level-1-b.txt'

 r←T._OK

∆TidyUp:
 Assert 0=1⊃F.RmDir path
⍝Done
